apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

repositories {
	mavenCentral()
	mavenLocal()
}

defaultTasks 'clean', 'build'

group = 'coyote'
version = '1.0.5-SNAPSHOT'


jar {
    manifest {
        attributes 'Provider': 'Coyote Systems', 
        'Implementation-Version': version
    }
}


// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}


// add javadoc/source jar tasks as artifacts
artifacts {
	archives sourcesJar, javadocJar
}


dependencies {
	// Unit testing, of course
	testImplementation 'junit:junit:4.12'
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			if (plugins.hasPlugin('war')) {
				from components.web
			} else {
				from components.java
			}

			artifact sourcesJar {
				classifier "sources"
			}

			artifact javadocJar {
				classifier "javadoc"
			}
		}
	}
	repositories {
		mavenLocal()
		maven {
			name = 'github'
			url = "https://maven.pkg.github.com/sdcote/packages"
			credentials {
				// get credentials from 'gradle.properties' if they exist or from the environment
				username = project.findProperty("USERNAME") ?: System.getenv("secrets.ACTIONS_USER")
				password = project.findProperty("TOKEN") ?: System.getenv("secrets.ACTIONS_TOKEN")
			}
		}
	}
}

jacocoTestReport {
   	reports {
       	xml.enabled = true
        html.enabled = true
   	}
}
check.dependsOn jacocoTestReport
